apply plugin: 'java'

ext {
    sharedManifest = manifest {
        attributes(
                'Build-Time': buildTime,
                'Implementation-Version': version,
                'Implementation-Vendor': 'Yurii Serhiichuk',
                'Specification-Version': version,
                'Specification-Vendor': 'Yurii Serhiichuk'
        )
    }
}

tasks.withType(JavaCompile.class) {
    options.deprecation = false
    options.warnings = false
    options.encoding = sourceEncoding
    options.compilerArgs = ['-Xmaxerrs', '20', '-Xmaxwarns', '0', '-Xlint:all']
    sourceCompatibility = project.ext.sourceCompatibility
    targetCompatibility = project.ext.targetCompatibility
}

javadoc {
    options.memberLevel = JavadocMemberLevel.PROTECTED
    classpath = configurations.compile
}

//create a single Jar with all dependencies
task generateOneJar(type: Jar) {
    getArchiveBaseName().set("${project.name}-all")
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

tasks.withType(Jar) { // includes War and Ear
    manifest = project.manifest {
        from sharedManifest
    }
}

/**
 * Sets {@code value} of the JAR manifest file attribute with specified {@code name}.
 */
def setJarManifestAttribute(name, value) {
    tasks.withType(Jar) { // includes War and Ear
        manifest.getAttributes().put(name, value)
    }
}

/**
 * Sets {@code Name} manifest attribute {@code value}.
 */
def setJarManifestName(value) {
    setJarManifestAttribute('Name', value)
}

/**
 * Sets {@code Specification-Title} manifest attribute {@code value}.
 */
def setJarManifestSpecificationTitle(value) {
    setJarManifestAttribute('Specification-Title', value)
}
/**
 * Sets {@code Implementation-Title} manifest attribute {@code value}.
 */
def setJarManifestImplementationTitle(value) {
    setJarManifestAttribute('Implementation-Title', value)
}

ext.setJarManifestImplementationTitle = this.&setJarManifestImplementationTitle
ext.setJarManifestSpecificationTitle = this.&setJarManifestSpecificationTitle
ext.setJarManifestName = this.&setJarManifestName
